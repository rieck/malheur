dnl matlabdir.m4 --- define Matlab directory names.
dnl
dnl Copyright (C) 2002, 2003 Ralph Schleicher
dnl
dnl This program is free software; you can redistribute it and/or
dnl modify it under the terms of the GNU General Public License as
dnl published by the Free Software Foundation; either version 2,
dnl or (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; see the file COPYING.  If not, write to
dnl the Free Software Foundation, Inc., 59 Temple Place, Suite 330,
dnl Boston, MA 02111-1307, USA.
dnl
dnl As a special exception to the GNU General Public License, if
dnl you distribute this file as part of a program that contains a
dnl configuration script generated by GNU Autoconf, you may include
dnl it under the same distribution terms that you use for the rest
dnl of that program.
dnl
dnl Code:

# AX_MATLAB_DIR
# -------------
# Define Matlab directory names.
AC_DEFUN([AX_MATLAB_DIR],
[dnl
AC_PREREQ([2.50])
AC_REQUIRE([AX_MATLAB_VERSION])
AC_REQUIRE([AC_CANONICAL_HOST])
ax_matlab_dir="\${datadir}/matlab"
AC_ARG_WITH([matlab-dir], AC_HELP_STRING([--with-matlab-dir=ARG], [use ARG as the top-level installation directory for Matlab files [[$(datadir)/matlab]]]),
[case $withval in
  yes | no)
    AC_MSG_ERROR([invalid value '$withval' for --with-matlab-dir])
    ;;
  *)
    ax_matlab_dir=$withval
    ;;
esac])
AC_MSG_CHECKING([for directory of Matlab files])
AC_MSG_RESULT([$ax_matlab_dir])
matlabdir=$ax_matlab_dir
AC_SUBST([matlabdir])
matlabverdir="\${matlabdir}/$MATLAB_VERSION"
AC_SUBST([matlabverdir])
matlabarchdir="\${matlabverdir}/$host"
AC_SUBST([matlabarchdir])
matlabprivatedir="\${matlabdir}/private"
AC_SUBST([matlabprivatedir])
matlabprivateverdir="\${matlabverdir}/private"
AC_SUBST([matlabprivateverdir])
matlabprivatearchdir="\${matlabarchdir}/private"
AC_SUBST([matlabprivatearchdir])
])

dnl matlabdir.m4 ends here
